## 完整代码

```js
Shader "Example02/07标准着色器"
{
    Properties
    {
        _MainColor ("MainColor",Color) =(1,1,1,1)
        _MainTex("MainTex",2d) ="white" {}
        _Gloss("Gloss",Range(0.03,1)) =0.5
        _BumpMap("NormalMap",2D) = "bump" { }
        _BumpScale("NormalScale",Range(-1,1)) = 1

        [KeywordEnum (Lambert,HalfLambert)] DIFFUSETYPE("漫反射", Float) = 0
        [KeywordEnum (Phong,Blinn)] SPECULARTYPE("反射", Float) = 0
    }
    SubShader
    {
        Tags 
        {
            "RenderType"="Opaque"  
            "LightMode"="ForwardBase"
        }

        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            #include "UnityCG.cginc"
            #include "Lighting.cginc"

            #pragma multi_compile SPECULARTYPE_PHONG SPECULARTYPE_BLINN
            #pragma multi_compile DIFFUSETYPE_LAMBERT DIFFUSETYPE_HALFLAMBERT
            
            //--- 计算封装 可直接写成cginc文件 ------------------------------
            fixed3 Lambert(fixed3 diffuseCol, fixed3 worldNormal, fixed3 lightDir)
            {
                return (_LightColor0.rgb * diffuseCol.rgb) * max(0,dot(worldNormal, lightDir));
            }
            fixed3 HalfLambert(fixed3 diffuseCol, fixed3 worldNormal, fixed3 lightDir )
            {
                return (_LightColor0.rgb * diffuseCol.rgb ) * max(0,dot(worldNormal, lightDir)* 0.5 + 0.5);
            }
            fixed3 SpecularPhong(fixed3 specularCol, fixed3 viewDir, fixed3 reflectDir,fixed gloss)
            {
                return (_LightColor0.rgb * specularCol.rgb) * pow(max(0,dot(viewDir,reflectDir)), gloss);
            }
            fixed3 SpecularBlinn(fixed3 specularCol,fixed3 worldNormal,fixed3 halfDir,fixed gloss)
            {
                return _LightColor0.rgb * specularCol.rgb * pow(max(0,dot(worldNormal, halfDir)), gloss);
            }
            //--- End ------------------------------
            struct v2f
            {
                float4 vertex : SV_POSITION;
                float2 texcoord: TEXCOORD0; // 贴图UV
                float3 worldPos: TEXCOORD1; // 世界坐标
                float3 tSpace0: TEXCOORD2;  // TNB矩阵0
                float3 tSpace1: TEXCOORD3;  // TNB矩阵1
                float3 tSpace2: TEXCOORD4;  // TNB矩阵2
            };

            fixed4 _MainColor;
            sampler2D _MainTex;
            float4 _MainTex_ST;
            sampler2D _BumpMap;
            fixed _BumpScale;
            fixed _Gloss;

            v2f vert (appdata_full v)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);                          // 顶点 => 裁剪空间
                o.worldPos = mul(unity_ObjectToWorld, v.vertex);                    // 顶点 => 世界坐标
                fixed3 worldNormal = UnityObjectToWorldNormal(v.normal);            // 法线 => 世界坐标
                fixed3 worldTangent = UnityObjectToWorldDir(v.tangent);             // 法线 => 世界坐标
                fixed3 worldBinormal = cross(worldNormal, worldTangent) * v.tangent.w * unity_WorldTransformParams.w;   // 副切线
                
                // 组合TBN矩阵 用于后续的切线空间法线计算。
                o.tSpace0 = float3(worldTangent.x, worldBinormal.x, worldNormal.x);
                o.tSpace1 = float3(worldTangent.y, worldBinormal.y, worldNormal.y);
                o.tSpace2 = float3(worldTangent.z, worldBinormal.z, worldNormal.z);
                o.texcoord = TRANSFORM_TEX(v.texcoord, _MainTex);   // UV缩放跟随主贴图
                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                fixed3 normal = UnpackNormal(tex2D(_BumpMap, i.texcoord)); 
                normal.xy *= _BumpScale;  // 计算法线强度
                
                //--- 切线空间法线（贴图）=> 世界空间法线 ------------------------------
                fixed3 worldNormal = normalize(half3(dot(i.tSpace0, normal), dot(i.tSpace1, normal), dot(i.tSpace2, normal)));
                fixed3 lightDir = normalize(UnityWorldSpaceLightDir(i.worldPos));   // 光源向量
                fixed3 viewDir = normalize(UnityWorldSpaceViewDir(i.worldPos));     // 视角向量
                fixed3 reflectDir = normalize(reflect(-lightDir,worldNormal));      // 反射向量
                fixed3 halfDir = normalize(lightDir + viewDir);                     // 半角方向
                fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz;                      // 环境光

                fixed3 col = tex2D(_MainTex, i.texcoord);

                #ifdef DIFFUSETYPE_LAMBERT 
                    fixed3 diffuse = Lambert(_MainColor,worldNormal,lightDir);       // 兰伯特
                #elif DIFFUSETYPE_HALFLAMBERT
                    fixed3 diffuse = HalfLambert(_MainColor,worldNormal,lightDir);   // 半兰伯特
                #endif

                #ifdef SPECULARTYPE_PHONG 
                    fixed3  specular = SpecularPhong(_MainColor,viewDir,reflectDir,_Gloss * 128);   // Phong反射
                #elif SPECULARTYPE_BLINN
                    fixed3  specular = SpecularBlinn(_MainColor,worldNormal,halfDir,_Gloss * 128);  // Blinn反射
                #endif

                col *= diffuse + specular + ambient;   // 混合计算
                return fixed4(col,1);
            }
            ENDCG
        }
    }
}
```

## 材质效果

![](img/标准做色器.png)

> 所有效果的组合Shader.到此章节进阶篇知识基本完结，后续工程会新增一些Shader但不写md文档了，尽量注释清楚。