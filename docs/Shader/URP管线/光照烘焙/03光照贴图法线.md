```js
Shader "URPExample/光照贴图法线"
{
    Properties
    {
        _BaseColor("Color",Color) = (1,1,1,1)
        _BaseMap("Texture",2D) = "white"{}
        _BumpMap("Normal", 2D) = "bump" {}
        _Cutoff("AlphaCutout", Range(0.0, 1.0)) = 0.5

        // BlendMode
        [HideInInspector] _Surface("__surface", Float) = 0.0
        [HideInInspector] _Blend("__blend", Float) = 0.0
        [HideInInspector] _AlphaClip("__clip", Float) = 0.0
        [HideInInspector] _SrcBlend("Src", Float) = 1.0
        [HideInInspector] _DstBlend("Dst", Float) = 0.0
        // [HideInInspector] _ZWrite("ZWrite", Float) = 1.0
        [HideInInspector] _Cull("__cull", Float) = 2.0

        // Editmode props
        [HideInInspector] _QueueOffset("Queue offset", Float) = 0.0
    }
    SubShader
    {
        Tags
        {
            "RenderPipeline"="UniversalRenderPipeline"
            "LightMode"="UniversalForward"
            "RenderType"="Opaque"
        }

        HLSLINCLUDE
        #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"        //类似UnityCG.iginc
        #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Lighting.hlsl"    //光照hlsl

        #pragma multi_compile _ _ADDITIONAL_LIGHTS_VERTEX _ADDITIONAL_LIGHTS  //附加灯光宏
        #pragma multi_compile _ _MAIN_LIGHT_SHADOWS         //主光源阴影宏
        #pragma multi_compile _ _MAIN_LIGHT_SHADOWS_CASCADE //主光源阴影宏
        #pragma multi_compile _ _SHADOWS_SOFT           //软阴影宏
        #pragma multi_compile LIGHTMAP_OFF LIGHTMAP_ON  //光照贴图宏
        #pragma multi_compile _ DIRLIGHTMAP_COMBINED    //光照球谐宏
        #pragma shader_feature _ _NORMALMAP             //光照法线宏
        
        #pragma shader_feature _ALPHATEST_ON
        #pragma shader_feature _ALPHAPREMULTIPLY_ON

        TEXTURE2D(_BaseMap);    SAMPLER(sampler_BaseMap);
        TEXTURE2D(_BumpMap);    SAMPLER(sampler_BumpMap);

        CBUFFER_START(UnityPerMaterial)
        float4 _BaseMap_ST;
        half4 _BaseColor;
        float _Cutoff;
        CBUFFER_END
        ENDHLSL

        pass
        {
            Tags
            {
                "LightMode"="UniversalForward"
                "RenderType"="TransparentCutout"
                "Queue"="AlphaTest"
            }
            HLSLPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            struct a2v
            {
                float4 positionOS : POSITION;
                float3 normalOS : NORMAL;
                float4 tangentOS : TANGENT;
                float2 texcoord : TEXCOORD;
                float2 lightmapUV : TEXCOORD1;
            };

            struct v2f
            {
                float4 positionCS : SV_POSITION;
                float3 positionWS : TEXCOORD0;
                float3 normalWS : TEXCOORD1;
                //光照贴图宏
                #ifdef LIGHTMAP_ON
                    DECLARE_LIGHTMAP_OR_SH(lightmapUV, vertexSH, 2);
                #endif
                //光照法线宏
                #if defined(_NORMALMAP)
                    half3 tangent : TEXCOORD3;
                    half3 bitangent : TEXCOORD4;
                #endif
                float2 texcoord : TEXCOORD5;
            };

            v2f vert(a2v v)
            {
                v2f o;
                o.positionCS = TransformObjectToHClip(v.positionOS.xyz);    //To裁剪空间
                o.positionWS = TransformObjectToWorld(v.positionOS.xyz);    //To世界空间
                o.texcoord = TRANSFORM_TEX(v.texcoord, _BaseMap);           //uv计算
                o.normalWS = TransformObjectToWorldNormal(v.normalOS.xyz);  //To世界空间
                //光照贴图宏
                #ifdef LIGHTMAP_ON
                    OUTPUT_LIGHTMAP_UV(v.lightmapUV, unity_LightmapST, o.lightmapUV);   //灯光贴图输入
                    OUTPUT_SH(o.normalWS, o.vertexSH);  //球谐函数输入
                #endif
                //光照法线宏
                #if defined(_NORMALMAP)
                    o.tangent = TransformObjectToWorldDir(v.tangentOS.xyz);  //To世界空间
                    o.bitangent = cross(o.normalWS, o.tangent) * v.tangentOS.w * unity_WorldTransformParams.w; //计算副切线
                #endif                
                return o;
            }

            half4 frag(v2f i):SV_TARGET
            {
                //光照法线计算
                #if defined(_NORMALMAP)
                    half3 normalTS = UnpackNormalScale(SAMPLE_TEXTURE2D(_BumpMap, sampler_BumpMap, i.texcoord),1);
                    half3 normalWS = TransformTangentToWorld(normalTS, half3x3(i.tangent, i.bitangent, i.normalWS));
                #else
                    half3 normalWS = normalize(i.normalWS);
                #endif
                
                Light light = GetMainLight(TransformWorldToShadowCoord(i.positionWS)); //获取主光源、阴影
                float3 lightDir = normalize(light.direction);   //光源向量
                // float3 normalWS = normalize(i.normalWS);     //归一化
                half3 ambient = SampleSH(normalWS);             //球谐光照 计算环境光



                //固有色
                half4 albedo = SAMPLE_TEXTURE2D(_BaseMap, sampler_BaseMap, i.texcoord) * _BaseColor;

                //漫反射
                half3 diffuse = (light.color * _BaseColor.rgb) * max(0, dot(normalWS, lightDir) * 0.5 + 0.5);

                //辅助照明
                half4 lightColor;
                int lightList = GetAdditionalLightsCount();
                for (int f = 0; f < lightList; f++)
                {
                    Light addlight = GetAdditionalLight(f, i.positionWS);
                    float3 lightDir = normalize(addlight.direction);
                    float lightAttenuation = addlight.distanceAttenuation * addlight.shadowAttenuation;
                    lightColor += half4(addlight.color, 1) * (dot(normalWS, lightDir) * 0.5 + 0.5) * lightAttenuation * albedo;
                }

                // //光照贴图混合计算
                #ifdef LIGHTMAP_ON
                    half3 bakedGI = 1;
                    bakedGI = SAMPLE_GI(i.lightmapUV,i.vertexSH, normalWS);
                    albedo.rgb *= light.shadowAttenuation;
                    albedo.rgb *= diffuse + bakedGI;
                #elif LIGHTMAP_OFF
                    float lightAttenuation = light.distanceAttenuation * light.shadowAttenuation;
                    half3 radiance = (diffuse * lightAttenuation ) + ambient;
                    albedo.rgb *= radiance + lightColor.rgb;
                #endif
                return float4(albedo.rgb, 1);
            }
            ENDHLSL
        }

        //阴影Pass
        pass
        {
            Tags
            {
                "LightMode"="ShadowCaster"
            }
            HLSLPROGRAM
            #pragma vertex vertshadow
            #pragma fragment fragshadow

            struct a2v
            {
                float4 positionOS : POSITION;
                float3 normalOS : NORMAL;
                float2 texcoord : TEXCOORD;
            };

            struct v2f
            {
                float4 positionCS : SV_POSITION;
                float3 positionWS : TEXCOORD0;
                float3 normalWS : TEXCOORD1;
            };

            v2f vertshadow(a2v v)
            {
                v2f o;
                Light MainLight = GetMainLight();
                o.positionWS = TransformObjectToWorld(v.positionOS.xyz);
                o.normalWS = TransformObjectToWorldNormal(v.normalOS.xyz);
                o.positionCS = TransformWorldToHClip(ApplyShadowBias(o.positionWS, o.normalWS, MainLight.direction));
                #if UNITY_REVERSED_Z
                    o.positionCS.z = min(o.positionCS.z, o.positionCS.w * UNITY_NEAR_CLIP_VALUE);
                #else
                    o.positionCS.z = max(o.positionCS.z,o.positionCS.w * UNITY_NEAR_CLIP_VALUE);
                #endif
                return o;
            }

            half4 fragshadow(v2f i):SV_Target
            {
                return 0;
            }
            ENDHLSL
        }
    }
    // CustomEditor "UnityEditor.Rendering.Universal.ShaderGUI.BakedLitShader"
}
```

