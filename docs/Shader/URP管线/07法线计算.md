## 法线计算

### 封装引用计算

```js
Shader "URPExample/法线计算"
{
    Properties
    {
        _BaseColor("Color",Color) = (1,1,1,1)
        _BaseMap("Texture",2D) = "white"{}
        _BumpMap("Normal Map",2D) = "bump" {}
        _BumpScale("NormalScale",Range(-1,1)) = 1
    }
    SubShader
    {
        Tags
        {
            "RenderPipeline"="UniversalRenderPipeline"
            "LightMode"="UniversalForward"
            "RenderType"="Opaque"
        }

        HLSLINCLUDE
        #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"        //类似UnityCG.iginc
        #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Lighting.hlsl"    //光照hlsl

        TEXTURE2D(_BaseMap);    SAMPLER(sampler_BaseMap);
        TEXTURE2D(_BumpMap);    SAMPLER(sampler_BumpMap);

        CBUFFER_START(UnityPerMaterial)
        float4 _BaseMap_ST;
        half4 _BaseColor;
        half _BumpScale;
        CBUFFER_END
        ENDHLSL

        pass
        {
            HLSLPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            struct a2v
            {
                float4 positionOS : POSITION;
                float3 normalOS : NORMAL;
                float4 tangentOS : TANGENT;
                float2 texcoord : TEXCOORD;
            };

            struct v2f
            {
                float4 positionCS : SV_POSITION;
                float3 positionWS : TEXCOORD0;
                half3 normalWS : TEXCOORD1;
                half3 tangent : TEXCOORD2;
                half3 bitangent : TEXCOORD3;
                float2 texcoord : TEXCOORD4;
            };

            v2f vert(a2v v)
            {
                v2f o;
                //获取顶点输入
                VertexPositionInputs vertexInput = GetVertexPositionInputs(v.positionOS.xyz);
                o.positionCS = vertexInput.positionCS;
                o.positionWS = vertexInput.positionWS;

                //获取法线输入
                VertexNormalInputs normalInput = GetVertexNormalInputs(v.normalOS, v.tangentOS);
                o.normalWS = normalInput.normalWS;
                o.tangent = normalInput.tangentWS;
                o.bitangent = normalInput.bitangentWS;
               
                //UV计算
                o.texcoord = TRANSFORM_TEX(v.texcoord, _BaseMap);
                return o;
            }

            half4 frag(v2f i):SV_Target
            {
                Light light = GetMainLight();                   //获取主灯光
                float3 lightDir = normalize(light.direction);   //归一化

                //开方法线
                half3 normalTS = UnpackNormalScale(SAMPLE_TEXTURE2D(_BumpMap, sampler_BumpMap, i.texcoord),_BumpScale);
                
                //切线转 To 世界空间
                half3 normalWS = TransformTangentToWorld(normalTS, half3x3(i.tangent, i.bitangent, i.normalWS));

                //固有色
                half4 albedo = SAMPLE_TEXTURE2D(_BaseMap, sampler_BaseMap, i.texcoord) * _BaseColor;
                
                //半兰伯特
                half3 diffuse = light.color * _BaseColor.rgb * max(0, dot(normalWS, lightDir) * 0.5 + 0.5);

                //混合计算
                albedo.rgb *= diffuse;
                return albedo;
            }
            ENDHLSL
        }
    }
}
```

`` #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"``的封装方法如果使用参数较多的情况下可以直接使用封装

````js
VertexPositionInputs GetVertexPositionInputs(float3 positionOS)
{
    VertexPositionInputs input;
    input.positionWS = TransformObjectToWorld(positionOS);
    input.positionVS = TransformWorldToView(input.positionWS);
    input.positionCS = TransformWorldToHClip(input.positionWS);
    
    float4 ndc = input.positionCS * 0.5f;
    input.positionNDC.xy = float2(ndc.x, ndc.y * _ProjectionParams.x) + ndc.w;
    input.positionNDC.zw = input.positionCS.zw;
        
    return input;
}

VertexNormalInputs GetVertexNormalInputs(float3 normalOS, float4 tangentOS)
{
    VertexNormalInputs tbn;

    // mikkts space compliant. only normalize when extracting normal at frag.
    real sign = tangentOS.w * GetOddNegativeScale();
    tbn.normalWS = TransformObjectToWorldNormal(normalOS);
    tbn.tangentWS = TransformObjectToWorldDir(tangentOS.xyz);
    tbn.bitangentWS = cross(tbn.normalWS, tbn.tangentWS) * sign;
    return tbn;
}
````

### 无封装计算

```js
Shader "URPExample/法线计算"
{
    Properties
    {
        _BaseColor("Color",Color) = (1,1,1,1)
        _BaseMap("Texture",2D) = "white"{}
        _BumpMap("Normal Map",2D) = "bump" {}
        _BumpScale("NormalScale",Range(-1,1)) = 1
    }
    SubShader
    {
        Tags
        {
            "RenderPipeline"="UniversalRenderPipeline"
            "LightMode"="UniversalForward"
            "RenderType"="Opaque"
        }

        HLSLINCLUDE
        #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"        //类似UnityCG.iginc
        #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Lighting.hlsl"    //光照hlsl

        TEXTURE2D(_BaseMap);    SAMPLER(sampler_BaseMap);
        TEXTURE2D(_BumpMap);    SAMPLER(sampler_BumpMap);

        CBUFFER_START(UnityPerMaterial)
        float4 _BaseMap_ST;
        half4 _BaseColor;
        half _BumpScale;
        CBUFFER_END
        ENDHLSL

        pass
        {
            HLSLPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            struct a2v
            {
                float4 positionOS : POSITION;
                float3 normalOS : NORMAL;
                float4 tangentOS : TANGENT;
                float2 texcoord : TEXCOORD;
            };

            struct v2f
            {
                float4 positionCS : SV_POSITION;
                float3 positionWS : TEXCOORD0;
                //TBN矩阵
                float3 tSpace0: TEXCOORD1;
                float3 tSpace1: TEXCOORD2;
                float3 tSpace2: TEXCOORD3;
                float2 texcoord : TEXCOORD4;
            };

            v2f vert(a2v v)
            {
                v2f o;
                o.positionCS = TransformObjectToHClip(v.positionOS.xyz);    //To裁剪空间
                o.positionWS = TransformObjectToWorld(v.positionOS.xyz);    //To世界空间

                float3 normalWS = TransformObjectToWorldNormal(v.normalOS.xyz); //To世界空间
                float3 tangent = TransformObjectToWorldDir(v.tangentOS.xyz);  //To世界空间
                float3 bitangent = cross(normalWS, tangent) * v.tangentOS.w * unity_WorldTransformParams.w; //计算副切线

                // 组合TBN矩阵 用于后续的切线空间法线计算。
                o.tSpace0 = float3(tangent.x, bitangent.x, normalWS.x);
                o.tSpace1 = float3(tangent.y, bitangent.y, normalWS.y);
                o.tSpace2 = float3(tangent.z, bitangent.z, normalWS.z);
                //UV计算
                o.texcoord = TRANSFORM_TEX(v.texcoord, _BaseMap);
                return o;
            }

            half4 frag(v2f i):SV_Target
            {
                Light light = GetMainLight();                   //获取主灯光
                float3 lightDir = normalize(light.direction);   //归一化

                //开方法线
                float3 normalTS = UnpackNormalScale(SAMPLE_TEXTURE2D(_BumpMap, sampler_BumpMap, i.texcoord), _BumpScale);
                
                //切线转 To 世界空间
                float3 normalWS = normalize(half3(dot(i.tSpace0, normalTS), dot(i.tSpace1, normalTS),dot(i.tSpace2, normalTS)));

                //固有色
                half4 albedo = SAMPLE_TEXTURE2D(_BaseMap, sampler_BaseMap, i.texcoord) * _BaseColor;

                //半兰伯特
                half3 diffuse = light.color * _BaseColor.rgb * max(0, dot(normalWS, lightDir) * 0.5 + 0.5);

                //混合计算
                albedo.rgb *= diffuse;
                return float4(albedo.rgb, 1);
            }
            ENDHLSL
        }
    }
}
```
