```js
Shader "URPExample/04高光反射"
{
    Properties
    {
        _BaseColor("Color",Color) = (1,1,1,1)
        _BaseMap("Texture",2D) = "white"{}
        _Gloss("Gloss",Range(0.03,1)) = 0.5
        [KeywordEnum (Phong,Blinn)] SPECULARTYPE("反射类型", Float) = 0 //宏定义
    }
    SubShader
    {
        Tags
        {
            "RenderPipeline"="UniversalRenderPipeline"
            "LightMode"="UniversalForward"
            "RenderType"="Opaque"
        }

        HLSLINCLUDE
        #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"        //类似UnityCG.iginc
        #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Lighting.hlsl"    //光照hlsl

        #pragma multi_compile SPECULARTYPE_PHONG SPECULARTYPE_BLINN  //自定义高光反射宏

        TEXTURE2D(_BaseMap);    SAMPLER(sampler_BaseMap);

        CBUFFER_START(UnityPerMaterial)
        float4 _BaseMap_ST;
        half4 _BaseColor;
        half _Gloss;
        CBUFFER_END
        ENDHLSL
        
        pass
        {
            HLSLPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            
            struct a2v
            {
                float4 positionOS : POSITION;
                float3 normalOS : NORMAL;
                float2 texcoord : TEXCOORD;
            };
            struct v2f
            {
                float4 positionCS : SV_POSITION;
                float3 positionWS : TEXCOORD0;
                float3 normalWS : NORMAL;
                float2 texcoord : TEXCOORD1;
            };
            
            //--- 光照封装方便宏调用  ------------------------------
            float3 SpecularPhong(float3 Lc,float3 Sc, float3 V, float3 R,float3 gloss){return Lc * Sc  * pow(max(0,dot(V,R)), gloss);}
            float3 SpecularBlinn(float3 Lc,float3 Sc,float3 N,float3 H,float3 gloss){return Lc * Sc  * pow(max(0,dot(N, H)), gloss);}

            v2f vert(a2v v)
            {
                v2f o;
                o.positionCS = TransformObjectToHClip(v.positionOS.xyz);    //To裁剪空间
                o.positionWS = TransformObjectToWorld(v.positionOS.xyz);    //To世界空间
                o.normalWS = TransformObjectToWorldNormal(v.normalOS.xyz);  //To世界空间
                o.texcoord = TRANSFORM_TEX(v.texcoord,_BaseMap);            //UV计算
                return o;
            }

            half4 frag(v2f i):SV_Target
            {
                Light light = GetMainLight();   //获取主灯光
                float3 lightDir = normalize(light.direction);       //归一化
                float3 normalWS = normalize(i.normalWS);            //归一化

                float3 viewDir = normalize(_WorldSpaceCameraPos.xyz -  i.positionWS);   //视角向量
                float3 reflectDir = normalize(reflect(-lightDir,normalWS));     //反射向量
                float3 halfDir = normalize(lightDir + viewDir);                 //半角方向
                
                //固有色
                half4 albedo = SAMPLE_TEXTURE2D(_BaseMap, sampler_BaseMap, i.texcoord) * _BaseColor;

                //高光
                float3 specularCol;
                #ifdef SPECULARTYPE_PHONG 
                    specularCol = SpecularPhong(light.color,_BaseColor.rbg,viewDir,reflectDir,_Gloss * 128);
                #elif SPECULARTYPE_BLINN
                    specularCol = SpecularBlinn(light.color,_BaseColor.rbg,normalWS,halfDir,_Gloss * 128);
                #endif

                //混合计算
                albedo.rgb *= specularCol;
                return float4(albedo.rgb,1);
            }
            ENDHLSL
        }
    }
}
```

