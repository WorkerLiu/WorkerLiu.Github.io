```js
Shader "URPExample/顶点漫反射"
{
    Properties
    {
        _BaseColor("Color",Color) = (1,1,1,1)
        _BaseMap("Texture",2D) = "white"{}
        [KeywordEnum (Lambert,HalfLambert)] DIFFUSETYPE("漫反射类型", Float) = 0
    }
    SubShader
    {
        Tags 
        {
            "RenderPipeline"="UniversalRenderPipeline"
            "LightMode"="UniversalForward"
            "RenderType"="Opaque"
        }

        HLSLINCLUDE
        #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"        //类似UnityCG.iginc
        #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Lighting.hlsl"    //光照hlsl
        
        #pragma multi_compile DIFFUSETYPE_LAMBERT DIFFUSETYPE_HALFLAMBERT    //自定义漫反射宏

        TEXTURE2D(_BaseMap);    SAMPLER(sampler_BaseMap);

        CBUFFER_START(UnityPerMaterial)
        float4 _BaseMap_ST;
        half4 _BaseColor;
        CBUFFER_END
        ENDHLSL
        
        pass
        {
            HLSLPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            
            struct a2v
            {
                float4 positionOS : POSITION;
                float3 normalOS : NORMAL;
                float2 texcoord : TEXCOORD;
            };
            struct v2f
            {
                float4 positionCS : SV_POSITION;
                float2 texcoord : TEXCOORD0;
                float3 color : COLOR;
            };

            //--- 光照封装方便宏调用  ------------------------------
            float3 Lambert(float3 Lc, float3 Dc, float3 N, float3 L){return (Lc * Dc) * max(0,dot(N, L));} 
            float3 HalfLambert(float3 Lc,float3 Dc, float3 N, float3 L){return (Lc * Dc) * max(0,dot(N, L) * 0.5 + 0.5);}
            
            v2f vert(a2v v)
            {
                v2f o;
                o.positionCS = TransformObjectToHClip(v.positionOS.xyz);    //To裁剪空间
                o.texcoord = TRANSFORM_TEX(v.texcoord,_BaseMap);            //UV计算
                
                Light light = GetMainLight();   //获取主灯光
                float3 lightDir = normalize(light.direction);   //光源向量
                float3 normalWS =TransformObjectToWorldNormal(v.normalOS.xyz);  //To世界空间

                //漫反射
                #ifdef DIFFUSETYPE_LAMBERT 
                    half3 diffuse = Lambert(light.color, _BaseColor.rgb, normalWS, lightDir);       //兰伯特
                #elif DIFFUSETYPE_HALFLAMBERT
                    half3 diffuse = HalfLambert(light.color, _BaseColor.rgb, normalWS, lightDir);   //半兰伯特
                #endif
                
                //环境颜色
                o.color = diffuse;

                return o;
            }

            half4 frag(v2f i):SV_Target
            {
                //固有色
                half4 albedo = SAMPLE_TEXTURE2D(_BaseMap, sampler_BaseMap, i.texcoord) * _BaseColor;
                
                //混合计算
                albedo.rgb *= i.color;
                return float4(albedo.rgb,1);
            }
            ENDHLSL
        }
    }
}
```

