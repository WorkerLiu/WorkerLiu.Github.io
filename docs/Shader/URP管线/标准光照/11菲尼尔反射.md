## 菲尼尔反射

```js
Shader "URPExample/菲尼尔反射"
{
    Properties
    {
        _BaseColor("Color",Color) = (1,1,1,1)
        _BaseMap("Texture",2D) = "white"{}
        _Scale("Scale",Range(0,1)) =1
        _Value("Value",Range(0,5)) =2.5
    }
    SubShader
    {
        Tags
        { 
            "RenderPipeline"="UniversalRenderPipeline"
            "LightMode"="UniversalForward"
            "RenderType"="Opaque"
        }

        HLSLINCLUDE
        #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"        //类似UnityCG.iginc
        #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Lighting.hlsl"    //光照hlsl
        

        TEXTURE2D(_BaseMap);    SAMPLER(sampler_BaseMap);

        CBUFFER_START(UnityPerMaterial)
        float4 _BaseMap_ST;
        half4 _BaseColor;
        float _Scale;
        float _Value;
        CBUFFER_END
        ENDHLSL
        
        pass
        {
            HLSLPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            
            struct a2v
            {
                float4 positionOS : POSITION;
                float3 normalOS : NORMAL;
                float2 texcoord : TEXCOORD;
            };

            struct v2f
            {
                float4 positionCS : SV_POSITION;
                float3 positionWS : TEXCOORD0;
                float3 normalWS : NORMAL;
                float2 texcoord : TEXCOORD1;
            };
            
            v2f vert(a2v v)
            {
                v2f o;
                o.positionCS = TransformObjectToHClip(v.positionOS.xyz);    //To裁剪空间
                o.positionWS = TransformObjectToWorld(v.positionOS.xyz);    //To世界空间
                o.normalWS =TransformObjectToWorldNormal(v.normalOS.xyz);   //To世界空间
                o.texcoord = TRANSFORM_TEX(v.texcoord,_BaseMap);            //UV计算
                return o;
            }

            half4 frag(v2f i):SV_Target
            {
                Light light = GetMainLight();                       //获取主灯光
                float3 lightDir = normalize(light.direction);       //光源向量
                float3 normalWS = normalize(i.normalWS);            //归一化
                float3 viewDir= normalize(_WorldSpaceCameraPos.xyz -  i.positionWS); //视角向量
                
                //固有色
                half4 albedo = SAMPLE_TEXTURE2D(_BaseMap, sampler_BaseMap, i.texcoord) * _BaseColor;

                //菲尼尔反射
                float fresnel = pow(abs((1 - dot(normalWS, viewDir))), _Value) * _Scale;

                //混合计算
                albedo.rgb += fresnel;
                return float4(albedo.rgb,1);
            }
            ENDHLSL
        }
    }
}
```