## 标准着色器

```js
Shader "URPExample/标准着色器"
{
    Properties
    {
        _BaseColor("Color",Color) = (1,1,1,1)
        _BaseMap("Texture",2D) = "white"{}
        _BumpMap("Normal Map",2D) = "bump" {}
        _BumpScale("NormalScale",Range(-1,1)) = 1
        _Gloss("Gloss",Range(0.03,1)) = 0.5
        
        [Space(30)]
        [KeywordEnum (Lambert,HalfLambert)] DIFFUSETYPE("漫反射类型", Float) = 0
        [KeywordEnum (Phong,Blinn)] SPECULARTYPE("反射类型", Float) = 0
    }
    SubShader
    {
        Tags
        {
            "RenderPipeline"="UniversalRenderPipeline"
            "LightMode"="UniversalForward"
            "RenderType"="Opaque"
        }
        
        HLSLINCLUDE
        #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"        //类似UnityCG.iginc
        #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Lighting.hlsl"    //光照hlsl

        #pragma multi_compile DIFFUSETYPE_LAMBERT DIFFUSETYPE_HALFLAMBERT   //自定义漫反射宏
        #pragma multi_compile SPECULARTYPE_PHONG SPECULARTYPE_BLINN         //自定义高光反射宏

        TEXTURE2D( _BaseMap);   SAMPLER(sampler_BaseMap);
        TEXTURE2D( _BumpMap);   SAMPLER(sampler_BumpMap);

        CBUFFER_START(UnityPerMaterial)
        float4 _BaseMap_ST;
        half4 _BaseColor;
        half _BumpScale;
        half _Gloss;
        CBUFFER_END
        ENDHLSL
        
        pass
        {
            HLSLPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            struct a2v
            {
                float4 positionOS : POSITION;
                float3 normalOS : NORMAL;
                float4 tangentOS : TANGENT;
                float2 texcoord : TEXCOORD;
            };
            struct v2f
            {
                float4 positionCS : SV_POSITION;
                float3 positionWS : TEXCOORD0;
                half3 normalWS : TEXCOORD1;
                half3 tangent : TEXCOORD2;
                half3 bitangent : TEXCOORD3;
                float2 texcoord : TEXCOORD4;
            };

            //--- 光照封装方便宏调用  ------------------------------
            float3 Lambert(float3 Lc, float3 Dc, float3 N, float3 L){return (Lc * Dc) * max(0,dot(N, L));} 
            float3 HalfLambert(float3 Lc,float3 Dc, float3 N, float3 L){return (Lc * Dc) * max(0,dot(N, L) * 0.5 + 0.5);}
            float3 SpecularPhong(float3 Lc,float3 Sc, float3 V, float3 R,float3 gloss){return Lc * Sc  * pow(max(0,dot(V,R)), gloss);}
            float3 SpecularBlinn(float3 Lc,float3 Sc,float3 N,float3 H,float3 gloss){return Lc * Sc  * pow(max(0,dot(N, H)), gloss);}
            
            v2f vert(a2v v)
            {
                v2f o;
                //获取顶点输入
                VertexPositionInputs vertexInput = GetVertexPositionInputs(v.positionOS.xyz);
                o.positionCS = vertexInput.positionCS;
                o.positionWS = vertexInput.positionWS;

                //获取法线输入
                VertexNormalInputs normalInput = GetVertexNormalInputs(v.normalOS, v.tangentOS);
                o.normalWS = normalInput.normalWS;
                o.tangent = normalInput.tangentWS;
                o.bitangent = normalInput.bitangentWS;
               
                //UV计算
                o.texcoord = TRANSFORM_TEX(v.texcoord, _BaseMap); 
                return o;
            }

            half4 frag(v2f i):SV_Target
            {
                Light light = GetMainLight();                   //获取主灯光
                float3 lightDir = normalize(light.direction);   //归一化

                //开方法线
                half3 normalTS = UnpackNormalScale(SAMPLE_TEXTURE2D(_BumpMap, sampler_BumpMap, i.texcoord),_BumpScale);
                
                //切线转 To 世界空间
                half3 normalWS = normalize(TransformTangentToWorld(normalTS, half3x3(i.tangent, i.bitangent, i.normalWS)));

                float3 viewDir = normalize(_WorldSpaceCameraPos.xyz -  i.positionWS);   //视角向量
                float3 reflectDir = normalize(reflect(-lightDir,normalWS)); //反射向量
                float3 halfDir = normalize(lightDir + viewDir);             //半角方向

                // 固有色
                half4 albedo = SAMPLE_TEXTURE2D(_BaseMap, sampler_BaseMap, i.texcoord) * _BaseColor;

                // 漫反射
                #ifdef DIFFUSETYPE_LAMBERT 
                    half3 diffuse = Lambert(light.color, _BaseColor.rgb, normalWS, lightDir);       //兰伯特
                #elif DIFFUSETYPE_HALFLAMBERT
                    half3 diffuse = HalfLambert(light.color, _BaseColor.rgb, normalWS, lightDir);   //半兰伯特
                #endif

                // 反射高光
                #ifdef SPECULARTYPE_PHONG 
                    half3 specularCol = SpecularPhong(light.color, _BaseColor.rgb,viewDir, reflectDir, _Gloss * 128);   //Phong反射
                #elif SPECULARTYPE_BLINN
                    half3 specularCol = SpecularBlinn(light.color, _BaseColor.rgb,normalWS, halfDir, _Gloss * 128);     //Blinn反射
                #endif

                // 混合计算
                albedo.rgb = albedo.rgb * diffuse + specularCol;                
                return float4(albedo.rgb,1);
            }
            ENDHLSL
        }
    }
}
```

