```js
Shader "URPExample/02复杂光照/03光照阴影"
{
    Properties
    {
        _BaseColor("Color",Color) = (1,1,1,1)
        _BaseMap("Texture",2D) = "white"{}
        [KeywordEnum(ON,OFF)]CUT("CUT",float) = 1
        _Cutoff("Cutoff",Range(0,1)) = 0
    }
    SubShader
    {
        Tags{"RenderPipeline"="UniversalRenderPipeline"}

        HLSLINCLUDE
        #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"
        #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Lighting.hlsl"


        #pragma multi_compile _ _ADDITIONAL_LIGHTS_VERTEX _ADDITIONAL_LIGHTS  //附加灯光宏
        #pragma multi_compile _ _MAIN_LIGHT_SHADOWS         //主光源阴影宏
        #pragma multi_compile _ _MAIN_LIGHT_SHADOWS_CASCADE //主光源阴影宏
        #pragma multi_compile _ _SHADOWS_SOFT   //软阴影宏

        #pragma multi_compile CUT_ON CUT_OFF    //自定义阴影裁剪宏

        TEXTURE2D(_BaseMap); SAMPLER(sampler_BaseMap);

        CBUFFER_START(UnityPerMaterial)
        float4 _BaseMap_ST;
        half4 _BaseColor;
        float _Cutoff;
        CBUFFER_END
        ENDHLSL
        
        pass
        {
            Tags
            {
                "LightMode"="UniversalForward"
                "RenderType"="TransparentCutout"
                "Queue"="AlphaTest"
            }
            HLSLPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            struct a2v
            {
                float4 positionOS : POSITION;
                float3 normalOS : NORMAL;
                float2 texcoord : TEXCOORD;
            };
            struct v2f
            {
                float4 positionCS : SV_POSITION;
                float3 positionWS : TEXCOORD0;
                float3 normalWS : TEXCOORD1;
                float2 texcoord : TEXCOORD2;
            };

            v2f vert(a2v v)
            {
                v2f o;
                o.positionCS = TransformObjectToHClip(v.positionOS.xyz);    //To裁剪空间
                o.positionWS = TransformObjectToWorld(v.positionOS.xyz);    //To世界空间
                o.texcoord = TRANSFORM_TEX(v.texcoord,_BaseMap);            //UV计算
                o.normalWS =TransformObjectToWorldNormal(v.normalOS.xyz);   //To世界空间
                return o;
            }

            half4 frag(v2f i):SV_TARGET
            {
                Light light = GetMainLight(TransformWorldToShadowCoord(i.positionWS));  //获取主光源、阴影
                float3 lightDir = normalize(light.direction);   //光源向量
                float3 normalWS = normalize(i.normalWS);        //归一化
                half3 ambient = SampleSH(normalWS);             //球谐光照 计算环境光

                //固有色
                half4 albedo = SAMPLE_TEXTURE2D(_BaseMap, sampler_BaseMap, i.texcoord) * _BaseColor;

                //漫反射
                half3 diffuse = (light.color * _BaseColor.rgb) * max(0,dot(normalWS, lightDir) * 0.5 + 0.5);

                //辅助照明
                half3 lightColor = 0;
                #ifdef _ADDITIONAL_LIGHTS
                    int pixelLightCount = GetAdditionalLightsCount();
                    for(int lightIndex = 0 ; lightIndex < pixelLightCount; ++lightIndex)
                    {
                        Light light = GetAdditionalLight(lightIndex, i.positionWS);
                        half3 attenuatedLightColor = light.color * light.distanceAttenuation;   //灯光衰减计算
                        // half NdotL = saturate(dot(normalWS, light.direction));                  //Lambert
                        half NdotL = saturate(dot(normalWS, light.direction) * 0.5 + 0.5);      //HalfLambert
                        lightColor += attenuatedLightColor * NdotL;
                    }
                #endif

                //AlphaTest
                #ifdef CUT_ON
                    clip(albedo.a-_Cutoff);
                #endif

                //混合计算
                float lightAttenuation = light.distanceAttenuation * light.shadowAttenuation;   //投影衰减
                albedo.rgb *= (diffuse * lightAttenuation) + lightColor + ambient;
                return float4(albedo.rgb,1);
            }
            ENDHLSL
        }
        
        //阴影Pass
        pass
        {
            Tags{"LightMode"="ShadowCaster"}
            HLSLPROGRAM
            #pragma vertex vertshadow
            #pragma fragment fragshadow

            struct a2v
            {
                float4 positionOS : POSITION;
                float3 normalOS : NORMAL;
                float2 texcoord : TEXCOORD;
            };
            struct v2f
            {
                float4 positionCS : SV_POSITION;
                float3 positionWS : TEXCOORD0;
                float3 normalWS : TEXCOORD1;
                float2 texcoord : TEXCOORD2;
            };
            
            v2f vertshadow(a2v v)
            {
                v2f o;
                Light MainLight = GetMainLight();
                o.texcoord = TRANSFORM_TEX(v.texcoord,_BaseMap);
                o.positionWS = TransformObjectToWorld(v.positionOS.xyz);
                o.normalWS = TransformObjectToWorldNormal(v.normalOS.xyz);
                o.positionCS = TransformWorldToHClip(ApplyShadowBias(o.positionWS,o.normalWS,MainLight.direction));
                #if UNITY_REVERSED_Z
                    o.positionCS.z = min(o.positionCS.z,o.positionCS.w * UNITY_NEAR_CLIP_VALUE);
                #else
                    o.positionCS.z = max(o.positionCS.z,o.positionCS.w * UNITY_NEAR_CLIP_VALUE);
                #endif
                return o;
            }

            half4 fragshadow(v2f i):SV_Target
            {
                #ifdef CUT_ON
                    float alpha = SAMPLE_TEXTURE2D(_BaseMap,sampler_BaseMap,i.texcoord).a;
                    clip(alpha -_Cutoff);
                #endif
                return 0;
            }
            ENDHLSL
        }
    }
}
```

